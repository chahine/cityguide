apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
  compileSdkVersion Versions.compileSdkVersion

  defaultConfig {
    applicationId "com.chahinem.cityguide"

    minSdkVersion Versions.minSdkVersion
    targetSdkVersion Versions.targetSdkVersion

    versionCode 1
    versionName "1.0"

    buildConfigField("String", "GOOGLE_DISTANCE_API_KEY", "\"" + GOOGLE_DISTANCE_API_KEY + "\"")

    manifestPlaceholders += [GOOGLE_API_KEY: GOOGLE_API_KEY]

    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }
  signingConfigs {
    debug {
      if (rootProject.file('debug.properties').exists()) {
        final def props = new Properties()
        props.load(new FileInputStream(rootProject.file('debug.properties')))

        storeFile file(props['DEBUG_STORE_FILE'])
        storePassword props['DEBUG_STORE_PASSWORD']
        keyAlias props['DEBUG_KEY_ALIAS']
        keyPassword props['DEBUG_KEY_PASSWORD']
      }
    }
  }
  buildTypes {
    debug {
      multiDexEnabled true
      signingConfig signingConfigs.debug
    }
    release {
      multiDexEnabled true
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
      signingConfig signingConfigs.debug
    }
  }
}

dependencies {
  implementation Deps.kotlinStdlib
  implementation Deps.appcompatV7
  implementation Deps.design
  implementation Deps.recyclerviewV7
  implementation Deps.constraintLayout

  implementation 'com.google.android.gms:play-services-places:11.8.0'
  implementation 'com.google.android.gms:play-services-location:11.8.0'

  Deps.arch.each { implementation it }
  Deps.archAP.each { kapt it }

  Deps.rx.each { implementation it }
  Deps.api.each { implementation it }

  api Deps.dagger
  kapt Deps.daggerCompiler

  implementation Deps.timber

  Deps.testLibs.each { testImplementation it }

  androidTestImplementation 'com.android.support.test:runner:1.0.2-alpha1'
  androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2-alpha1'
}
